/**
 * @author zero-plusplus
 * @licence MIT
 * @see https://github.com/zero-plusplus/vscode-operate-from-autohotkey
 */
/**
 * **Note: To use this function, you need to have [vscode-operate-from-autohotkey](https://github.com/zero-plusplus/vscode-operate-from-autohotkey) installed on your [VSCode](https://code.visualstudio.com/).**
 * **Also, this function uses the clipboard that in order to cooperate with VSCode. Therefore, if you rewrite the clipboard before or after using this, it may not work correctly.**
 *
 * Execute the VSCode command. If you are using a version lower than v2.0-a110, please rename A_Clipboard to Clipboard.
 * @param {string} commandName -
 *  A string to indicate vscode's command name. You can check it in VSCode's Keyboard Shortcuts (`Ctrl+K`, `Ctrl+S`).
 *
 *  If you want to execute more than one command, separate them with `,`, such as `"command1, command2"`. Any whitespace before or after will be ignored.
 *  Also, If you want to execute a command multiple times, specify such as `"commandName:N"`, where `N` is an arbitrary integer.
 * @param {number} [timeout_ms := 1000] - Timeout (in milliseconds). Basically, there is no need to set this.
 * @return {string} - The return value of the command (string only). Otherwise, empty string.
 * @example
 *  ExecuteVsCodeCommand("cursorRight")             ; Move the cursor to the right
 *  ExecuteVsCodeCommand("cursorRight:5")           ; Execute the same command five times
 *  ExecuteVsCodeCommand("cursorRight, cursorDown") ; Execute multiple commands
 */
ExecuteVsCodeCommand(commandName, timeout_ms := 1000) {
  backup := ClipboardAll()
  A_Clipboard := commandName
  SendInput("+^!{F12}")

  ; Wait until the return value of the command can be obtained.
  startTime := A_TickCount
  while (true) {
    if (A_Clipboard != commandName) {
      break
    }

    elapsedTime := A_TickCount - startTime
    if (timeout_ms < elapsedTime) {
      throw Exception("Timeout. " commandName " was executed.")
    }
    Sleep(1)
  }

  result := A_Clipboard
  A_Clipboard := backup
  return result
}